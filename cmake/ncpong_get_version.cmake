find_package(Git)
if(GIT_EXECUTABLE)
	execute_process(
		COMMAND ${GIT_EXECUTABLE} rev-list --count HEAD
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		OUTPUT_VARIABLE GIT_REV_COUNT
		OUTPUT_STRIP_TRAILING_WHITESPACE
	)

	execute_process(
		COMMAND ${GIT_EXECUTABLE} rev-parse --short HEAD
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		OUTPUT_VARIABLE GIT_SHORT_HASH
		OUTPUT_STRIP_TRAILING_WHITESPACE
	)

	execute_process(
		COMMAND ${GIT_EXECUTABLE} log -1 --format=%ad --date=short
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		OUTPUT_VARIABLE GIT_LAST_COMMIT_DATE
		OUTPUT_STRIP_TRAILING_WHITESPACE
	)

	execute_process(
		COMMAND ${GIT_EXECUTABLE} describe --tags --exact-match HEAD
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		RESULT_VARIABLE GIT_NO_TAG
		OUTPUT_VARIABLE GIT_TAG_NAME
		ERROR_QUIET
		OUTPUT_STRIP_TRAILING_WHITESPACE
	)

	if(${GIT_NO_TAG})
		string(REPLACE "-" ";" GIT_LAST_COMMIT_DATE ${GIT_LAST_COMMIT_DATE})
		list(GET GIT_LAST_COMMIT_DATE 0 NCPONG_MAJOR_VERSION)
		list(GET GIT_LAST_COMMIT_DATE 1 NCPONG_MINOR_VERSION)
		set(NCPONG_PATCH_VERSION "r${GIT_REV_COUNT}-${GIT_SHORT_HASH}")
	endif()
else()
	string(TIMESTAMP NCPONG_MAJOR_VERSION "%Y")
	string(TIMESTAMP NCPONG_MINOR_VERSION "%m")
	string(TIMESTAMP NCPONG_PATCH_VERSION "%d")
endif()

if(NOT GIT_EXECUTABLE OR GIT_NO_TAG)
	set(NCPONG_VERSION "${NCPONG_MAJOR_VERSION}.${NCPONG_MINOR_VERSION}.${NCPONG_PATCH_VERSION}")
else()
	set(NCPONG_VERSION "${GIT_TAG_NAME}")
endif()
message(STATUS "ncPong version: ${NCPONG_VERSION}")

mark_as_advanced(NCPONG_MAJOR_VERSION NCPONG_MINOR_VERSION NCPONG_PATCH_VERSION NCPONG_VERSION)
